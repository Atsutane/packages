# Maintainer: Thorsten Toepper <atsutane at freethoughts dot de>
# Contributor: Thorsten Toepper <atsutane at freethoughts dot de>

pkgname=i3-git
pkgver=20090806
pkgrel=1
pkgdesc="An improved dynamic tiling window manager"
arch=('i686' 'x86_64')
url="http://i3.zekjur.net/"
license=('BSD')
provides=('i3')
conflicts=('i3')
depends=('xcb-util' 'libx11' 'libev')
makedepends=('git' 'asciidoc' 'docbook-xsl' 'pkgconfig')
optdepends=('dmenu' 'i3lock' 'i3status')
options=('docs' '!strip')

_gitroot="git://code.stapelberg.de/i3"
_gitname="i3"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
    
    cd $_gitname
    git branch -f next origin/next
    git checkout next
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make || return 1
  make -C man || return 1
  make DESTDIR="$pkgdir/" install || return 1

  install -Dm644 man/i3.1 \
    ${pkgdir}/usr/share/man/man1/i3.1 || return 1
  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}

# vim:set ts=2 sw=2 et:


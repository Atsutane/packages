# Maintainer: Thorsten Toepper <atsutane at freethoughts dot de>
# Contributor: Thorsten Toepper <atsutane at freethoughts dot de>

pkgname=i3status-git
pkgver=20090824
pkgrel=1
pkgdesc="Generates status bar to use with dzen2 or wmii"
arch=('i686' 'x86_64')
url="http://i3.zekjur.net/i3status/"
license=('BSD')
depends=('glibc')
optdepends=("dzen2-svn: As statusbar, dzen2 stable doesn't support -dock yet.")
makedepends=('git' 'pkgconfig')
options=('docs')
conflicts=('i3status')
provides=('i3status')

_gitroot="git://code.stapelberg.de/i3status"
_gitname="i3status"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # Comment this out if you want to use i3status with wmii
  echo "CFLAGS+=-DDZEN" >> Makefile

  make || return 1
}

package() {
  cd "$srcdir/$_gitname-build"

  make DESTDIR="$pkgdir" install || return 1

  install -Dm644 i3status.1 \
    ${pkgdir}/usr/share/man/man1/i3status.1 || return 1
  install -Dm644 debian/copyright \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  make clean
}

# vim:set ts=2 sw=2 et:


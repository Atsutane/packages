# Maintainer: Thorsten Toepper <atsutane at freethoughts dot de>
# Contributor: Thorsten Toepper <atsutane at freethoughts dot de>

pkgname=i3lock-git
pkgver=20090824
pkgrel=1
pkgdesc="Slightly improved version of slock"
arch=('i686' 'x86_64')
url="http://i3.zekjur.net/i3lock/"
license=('MIT')
depends=('libxpm' 'pam')
provides=('i3lock')
conflicts=('i3lock')
makedepends=('git')

_gitroot="git://code.stapelberg.de/i3lock"
_gitname="i3lock"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  cp -r "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  sed -i 's+/usr/X11R6+/usr+' config.mk || return 1

  make || return 1
}

package() {  
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir" install || return 1

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1

  make clean
}

# vim:set ts=2 sw=2 et:


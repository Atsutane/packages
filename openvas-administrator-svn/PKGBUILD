# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>

pkgname='openvas-administrator-svn'
pkgver=14915
pkgrel=1
pkgdesc='The OpenVAS administrator'
arch=('i686' 'x86_64')
url='http://www.openvas.org'
license=('GPL')
groups=('openvas-vcs')
conflicts=('openvas-administrator')
provides=('openvas-administrator')
replaces=('openvas-administrator')
depends=('openvas-libraries-svn')
makedepends=('subversion' 'cmake')
options=('!strip')

_svntrunk='https://svn.wald.intevation.org/svn/openvas/trunk/openvas-administrator'
_svnmod='openvas-administrator'

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"

  cd "$srcdir/$_svnmod-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var \
    -DLIBDIR=/usr/lib .
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

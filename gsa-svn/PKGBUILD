# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>
# gsa-no-werror-remove.patch taken from the gsa package from Vlatko Kosturjak

pkgname='gsa-svn'
pkgver=14525
pkgrel=1
pkgdesc='Greenbone Security Assistant - a web application for OpenVAS'
arch=('i686' 'x86_64')
url='http://www.openvas.org'
license=('GPL')
groups=('openvas-vcs')
depends=('openvas-libraries-svn' 'libmicrohttpd' 'glib2')
makedepends=('subversion' 'cmake')
options=('!strip')
source=('gsa-no-werror-remove.patch')
md5sums=('8a564ebe827c07ac8c1b9d1f4eab9eee')


_svntrunk='https://svn.wald.intevation.org/svn/openvas/trunk/gsa'
_svnmod='gsa'

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"

  cd "$srcdir/$_svnmod-build"

  patch -p1 < "${srcdir}/gsa-no-werror-remove.patch"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var \
    -DLIBDIR=/usr/lib .
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

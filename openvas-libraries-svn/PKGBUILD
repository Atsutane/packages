# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>
# Patch taken from the openvas-libraries package at AUR made by
# Vlatko Kosturjak, Mike Roberts and originally by Ermak.

pkgname='openvas-libraries-svn'
pkgver=15692
pkgrel=1
pkgdesc='The OpenVAS libraries'
arch=('i686' 'x86_64')
url='http://www.openvas.org'
license=('GPL')
groups=('openvas-vcs')
conflicts=('openvas-libraries')
provides=('openvas-libraries')
replaces=('openvas-libraries')
depends=('gnutls' 'libpcap' 'gpgme' 'bison')
makedepends=('subversion' 'cmake')
options=('!strip')
source=('openvas-nowerror-base.patch' 'openvas-nowerror-misc.patch'
        'openvas-nowerror-omp.patch' 'openvas-nowerror-hg.patch'
        'openvas-nowerror-nasl.patch')
md5sums=('c22f681edc9ebea41b36c0d25f3d3948'
         'f4e3baf3402ce66f843edbb127df474e'
         'd48d145e4f08d51ba3764cd7f4436b6d'
         '0da9569f95c63301b0b55daa5881487e'
         '3fd75873c17588d0375abc8ca42da399')

_svntrunk='https://svn.wald.intevation.org/svn/openvas/trunk/openvas-libraries'
_svnmod='openvas-libraries'

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."
  
  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"

  cd "$srcdir/$_svnmod-build"

  # Patch for the gnutls werrors
  patch -p0 < "$srcdir/openvas-nowerror-base.patch"
  patch -p0 < "$srcdir/openvas-nowerror-hg.patch"
  patch -p0 < "$srcdir/openvas-nowerror-misc.patch"
  patch -p0 < "$srcdir/openvas-nowerror-nasl.patch"
  patch -p0 < "$srcdir/openvas-nowerror-omp.patch"

  # Also set the LIBDIR specifically
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var \
    -DLIBDIR=/usr/lib .
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:

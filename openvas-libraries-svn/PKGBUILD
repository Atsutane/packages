# Maintainer: Thorsten TÃ¶pper <atsutane-aur@freethoughts.de>
# Patch taken from the openvas-libraries package at AUR made by
# Vlatko Kosturjak, Mike Roberts and originally by Ermak.

pkgname='openvas-libraries-svn'
pkgver=14397
pkgrel=1
pkgdesc='The OpenVAS libraries'
arch=('i686' 'x86_64')
url='http://www.openvas.org'
license=('GPL')
groups=('openvas-vcs')
depends=('gnutls' 'libpcap' 'gpgme' 'bison')
makedepends=('subversion' 'cmake')
options=('!strip')
source=('/openvas-nowerror.patch')
md5sums=('fbef16c2368d70b1e1de99b5fcd4d0c8')

_svntrunk='https://svn.wald.intevation.org/svn/openvas/trunk/openvas-libraries'
_svnmod='openvas-libraries'

build() {
  cd "$srcdir"
  msg "Connecting to SVN server...."

  if [[ -d "$_svnmod/.svn" ]]; then
    (cd "$_svnmod" && svn up -r "$pkgver")
  else
    svn co "$_svntrunk" --config-dir ./ -r "$pkgver" "$_svnmod"
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_svnmod-build"
  svn export "$srcdir/$_svnmod" "$srcdir/$_svnmod-build"

  cd "$srcdir/$_svnmod-build"

  # Patch for the gnutls werrors
  patch -p1 < "$srcdir/openvas-nowerror.patch"

  # Also set the LIBDIR specifically
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var \
    -DLIBDIR=/usr/lib .
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
